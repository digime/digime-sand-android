/*
 * Copyright (c) 2009-2018 digi.me Limited. All rights reserved.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "me.digi.sand.app1"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 5
        versionName "1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"


        /*
         * Consent Access Configuration parameters:
         * The following lines set parameters necessary for initialization and usage of the CA API (through SDK):
         * App ID, Contract ID, Private Key file and Private Key file password if any;
         * These are helper resource constructors which get injected into AndroidManifest.xml
         */
        resValue "string", "APP_ID", "your_real_app_id" // <- sample: "gDdQfubl66rj07webI92m4afPL9va9Z1" (32 characters hex string given by digi.me support)
        resValue "string", "P12_KEYSTORE_PASSWORD", "your_real_key_password" // <- sample: "20ov;p-1Fi3oL" (the password that goes with your private key). You will need to request the key password from dev support if using pre-set contract ID.

        resValue "string", "CONTRACT_ID", "your_real_contract_id" // <- demo contract ID for testing purposes
        resValue "string", "P12_KEYSTORE_FILENAME", "your_contract_id_filename" // <- demo private key in assets folder, accompanying the demo contract ID
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            def buildTypeName = variant.buildType.name
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    outputFileName = "SAND_" + flavorName + "_" +buildTypeName + "_" + defaultConfig.versionName + ".apk"
                    println 'OutputFile : ' + outputFileName
                }
            }
        }
    }

    flavorDimensions "env"

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'me.digi.sdk:digime-core:1.3.1'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'uk.co.chrisjenx:calligraphy:2.2.0'
    implementation 'com.android.support:support-v4:27.1.0'
    testImplementation 'junit:junit:4.12'
}
